{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "a6a4e15c",
   "metadata": {},
   "outputs": [],
   "source": [
    "using ITensors\n",
    "using ITensorMPS\n",
    "using LinearAlgebra\n",
    "using PolyChaos\n",
    "using QuadGK\n",
    "using Plots"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "991dfd0e",
   "metadata": {},
   "outputs": [],
   "source": [
    "function spectral_func(input::AbstractString, D::Float64, g::Float64)\n",
    "    inband = x -> (-D <= x <= D)\n",
    "        \n",
    "    if input == \"flat\"\n",
    "        J = x -> inband(x) ? 1/(2D) : 0.0\n",
    "    elseif input == \"elliptical\"\n",
    "        J = x -> inband(x) ? sqrt(1 - (x/D)^2) : 0.0\n",
    "    elseif input == \"ohmic\"\n",
    "        J = x -> inband(x) ? abs(x) : 0.0\n",
    "    elseif input == \"lorentzian\"\n",
    "        J = x -> 1/(1 + (x/D)^2)\n",
    "    else\n",
    "        error(\"spectral function type not recognized\")\n",
    "    end\n",
    "    # normalization\n",
    "    norm = quadgk(J, -D, D)[1]           \n",
    "    Jnorm = x -> g*D/pi * J(x) / norm\n",
    "    return Jnorm\n",
    "end\n",
    "\n",
    "function thermofield_transform(J, beta::Float64, mu::Float64) #spectral function, inverse temp, chemical potential\n",
    "    \"\"\"thermofield purification using fermi function ancilla\"\"\"\n",
    "    fermi(k) = 1/(1 + exp(beta*k - beta*mu))\n",
    "    J1 = w -> J(w) * fermi(w) #filled mode spectral density\n",
    "    J2 = w -> J(w) * (1 - fermi(w)) #empty mode spectral density\n",
    "    return J1, J2\n",
    "end\n",
    "\n",
    "function chain_map(J, N::Int64, D::Float64)\n",
    "    \"\"\"calculates family of monic orthogonal polynomials w.r.t the measure J(x) up to the Nth term.\n",
    "    returns the coefficients alpha and beta from the recurrence relation of the family.\"\"\"\n",
    "    supp = (-D, D)\n",
    "    meas = Measure(\"bath\", J, supp, false, Dict())\n",
    "    ortho_poly = OrthoPoly(\"bath_op\", N, meas; Nquad=10000)   \n",
    "    chain = coeffs(ortho_poly)                                  \n",
    "    Es = chain[1:N,1] #site energies\n",
    "    ts = sqrt.(chain[1:N,2]) #site hoppings (first term is system hopping)\n",
    "    return Es, ts\n",
    "end\n",
    "\n",
    "function prepare_2body_MPS(N::Int64, sys_state::Vector{String})\n",
    "    \"\"\"\n",
    "    Prepare initial MPS state for 2-body system + Bath\n",
    "    Sites 1 & 2: System\n",
    "    Sites 3 to N+2: Filled chain\n",
    "    Sites N+3 to 2N+2: Empty chain\n",
    "    \"\"\"\n",
    "    \n",
    "    # Using \"Fermion\" sites as in your snippet, conserving particle number\n",
    "    sites = siteinds(\"Fermion\", 2N+2, conserve_qns=true)\n",
    "    \n",
    "    # Initialize state array\n",
    "    # Default: Bath is empty (\"Emp\"), System is set by sys_state\n",
    "    states = [\"Occ\" for _ in 1:2N+2]\n",
    "    states[4:2:2N+2] .= \"Emp\"\n",
    "    \n",
    "    # Set System States (Site 1 and 2)\n",
    "    states[1] = sys_state[1]\n",
    "    states[2] = sys_state[2]\n",
    "    \n",
    "    # Optional: Fill half the bath if needed (Fermi sea)\n",
    "    # for j in 3:2:N_total\n",
    "    #     states[j] = \"Occ\"\n",
    "    # end\n",
    "\n",
    "    psi0 = MPS(sites, states) \n",
    "    return psi0, sites, states\n",
    "end\n",
    "\n",
    "function HamiltonianMPO(E_sys, t_sys, V_sys, E1, t1, E2, t2, sites, N)\n",
    "    \n",
    "    \"\"\"\n",
    "    1 - filled chain\n",
    "    2 - empty chain\n",
    "\n",
    "    Fermionic Hamiltonian for 2-body system coupled to a bath chain\n",
    "    System: Sites 1, 2\n",
    "    Bath: Sites 3...2N+2\n",
    "    \"\"\"\n",
    "\n",
    "    N_total = length(sites)\n",
    "    ampo = AutoMPO() \n",
    "    \n",
    "    # --- 1. Internal System Dynamics (Sites 1 & 2) ---\n",
    "    # Hopping between the two bodies\n",
    "    add!(ampo, E_sys[1], \"N\", 1)\n",
    "    add!(ampo, E_sys[2], \"N\", 2)\n",
    "\n",
    "    add!(ampo, t_sys, \"Cdag\", 1, \"C\", 2)\n",
    "    add!(ampo, t_sys, \"Cdag\", 2, \"C\", 1)\n",
    "    \n",
    "    # Interaction term (e.g., Coulomb repulsion V * n1 * n2)\n",
    "    add!(ampo, V_sys, \"N\", 1, \"N\", 2)\n",
    "    \n",
    "    #system-filled chain coupling\n",
    "    add!(ampo, t1[1], \"Cdag\", 2, \"C\", 3)\n",
    "    add!(ampo, t1[1], \"Cdag\", 3, \"C\", 2)\n",
    "\n",
    "    #system-empty chain coupling\n",
    "    add!(ampo, t2[1], \"Cdag\", 2, \"C\", 4)\n",
    "    add!(ampo, t2[1], \"Cdag\", 4, \"C\", 2)\n",
    "    \n",
    "    for j in 1:N\n",
    "        add!(ampo, E1[j], \"N\", 2 + 2j-1)\n",
    "        add!(ampo, E2[j], \"N\", 2 + 2j)\n",
    "    end\n",
    "    for j in 2:N\n",
    "        add!(ampo, t1[j], \"Cdag\", 2j-1, \"C\", 2j+1 )\n",
    "        add!(ampo, t1[j], \"Cdag\", 2j+1, \"C\", 2j-1)\n",
    "        add!(ampo, t2[j], \"Cdag\", 2j, \"C\", 2j+2)\n",
    "        add!(ampo, t2[j], \"Cdag\", 2j+2, \"C\", 2j)\n",
    "    end\n",
    "    return MPO(ampo, sites)\n",
    "end\n",
    "\n",
    "function evolve_MPS(psi0::MPS, H::MPO, dt::Float64, tmax::Float64)\n",
    "    \"\"\"Time evolve MPS with Hamiltonian MPO using TDVP\"\"\"\n",
    "    psi = psi0\n",
    "    ts = collect(dt:dt:tmax)\n",
    "    len = length(ts)\n",
    "    nSys1 = zeros(len)\n",
    "    nSys2 = zeros(len)\n",
    "    num = zeros(2N+1)\n",
    "    for k in 1:len\n",
    "        psi = tdvp(H, -im*dt, psi; nsite=2, outputlevel=1, mindim=1, maxdim=1000) #time_step=dt, nsweeps=sweeps, order=2)\n",
    "        nSys1[k] = expect(psi, \"N\"; sites=1)[1]\n",
    "        nSys2[k] = expect(psi, \"N\"; sites=2)[1]\n",
    "        println(\"timestep $k of $len complete\")\n",
    "    end\n",
    "    return nSys1, nSys2\n",
    "end\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "af3ba514",
   "metadata": {},
   "outputs": [],
   "source": [
    "D = 1.0\n",
    "mu = 0.0\n",
    "N = 50\n",
    "g = 0.1\n",
    "beta = 10.0\n",
    "\n",
    "E_sys = [0.0, 0.0] #system site energies\n",
    "t_sys = 0.05 #system site hopping\n",
    "V_sys = 0.1 #coulomb repulsion\n",
    "\n",
    "dt = 0.1\n",
    "tmax = 20.0\n",
    "\n",
    "\n",
    "J = spectral_func(\"elliptical\", D, g)\n",
    "J1, J2 = thermofield_transform(J, beta, mu)\n",
    "E1, t1 = chain_map(J1, N, D)\n",
    "E2, t2 = chain_map(J2, N, D)\n",
    "\n",
    "psi0, sites, states = prepare_2body_MPS(N, [\"Occ\", \"Emp\"])\n",
    "H = HamiltonianMPO(E_sys, t_sys, V_sys, E1, t1, E2, t2, sites, N)\n",
    "\n",
    "nSys1, nSys2 = evolve_MPS(psi0, H, dt, tmax)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "421bf5d9",
   "metadata": {},
   "outputs": [],
   "source": [
    "p1 = plot(collect(dt:dt:tmax), nSys1, label=\"Site 1 Occupation\", xlabel=\"Time\", ylabel=\"Occupation\", title=\"\\$ \\\\beta=$beta, \\\\mu=$mu, g=$g, t_{sys} = $t_sys, V=$V_sys\\$\")\n",
    "plot!(p1, collect(dt:dt:tmax), nSys2, label=\"Site 2 Occupation\", dpi=600)\n",
    "\n",
    "display(p1)\n",
    "savefig(\"twosite.png\")"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.11.7",
   "language": "julia",
   "name": "julia-1.11"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.11.7"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
